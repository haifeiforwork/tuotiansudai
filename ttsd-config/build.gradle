import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
        classpath group: 'com.cdancy', name: 'gradle-etcd-rest-plugin', version: '0.0.3', changing: true
    }
}

apply plugin: 'java'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'gradle-etcd-rest-plugin'

version = '1.0'

configurations {
    providedCompile
}

sourceSets {
    main.compileClasspath += configurations.providedCompile
    test.compileClasspath += configurations.providedCompile
    test.runtimeClasspath += configurations.providedCompile
}

processResources.outputs.upToDateWhen { false }

dependencies {
    compile 'mysql:mysql-connector-java:5.1.39',
            'com.coreos:jetcd-core:0.0.1',
            'log4j:log4j:1.2.17'
}

processResources {
    def innerConfigPath = "${project.projectDir.getPath()}/src/main/resources"
    def outerConfigPath = "${configPath}/ttsd-config"
    if (new File(outerConfigPath).isDirectory()) {
        innerConfigPath = outerConfigPath
    }
    from("${innerConfigPath}/ttsd-env.properties") {
        filter { String line ->
            if (project.hasProperty("dbhost") && line.startsWith("common.jdbc.host=")) {
                return "common.jdbc.host=${dbhost}"
            }

            if (project.hasProperty("dbport") && line.startsWith("common.jdbc.port=")) {
                return "common.jdbc.port=${dbport}"
            }

            if (project.hasProperty("redishost") && line.startsWith("common.redis.host=")) {
                return "common.redis.host=${redishost}"
            }

            if (project.hasProperty("redisport") && line.startsWith("common.redis.port=")) {
                return "common.redis.port=${redisport}"
            }
            line
        }
        into '/'
    }
    from("${innerConfigPath}/ttsd-biz.properties") {
        into '/'
    }
}

/**
 * parameter: dbhost dbport database
 * dbhost default value: ttsd-env.properties common.jdbc.host
 * dbport default value: ttsd-env.properties common.jdbc.port
 * database default value: aa
 * example: gradle -Pdbhost=192.168.33.10 -Pdbport=3306 -Pdatabase=aa ttsd-config:flywayMigrate
 *
 */
flyway {
    outOfOrder = true
    validateOnMigrate = false
    locations = ["filesystem:db_migration/${database}"]
    schemas = ["${database}"]

    url = "${env}" == "PRODUCTION" ? "jdbc:mysql://${host}:${port}/${database}?useUnicode=true&characterEncoding=UTF-8" : "jdbc:mysql://${host}:${port}?useUnicode=true&characterEncoding=UTF-8"
    user = "${username}"
    password = "${pwd}"
    println password
}
